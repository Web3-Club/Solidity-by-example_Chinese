# 访问私有数据

## 漏洞

智能合约上的所有数据都是公开的。

让我们看看如何阅读`private`数据。与此同时我们将初步学习 Solidity 的存储结构。

```solidity
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.24;

/*
合约部署在Goerli的地址为：
0x534E4Ce0ffF779513793cfd70308AF195827BD31

*/

/*
# Solidity存储布局
- Solidity的存储结构为插槽结构，最大2^256个插槽
- 每个插槽能存储32byte（256位）
- 变量的存储位置按照代码声明的顺序排列
- 为了优化存储空间的利用，如果相邻的变量可以没超过32 字节那么它们就会被打包到同一个槽中。

译者注：此处描述比较抽象，可以移步此处文章结合图片更深入理解:
https://learnblockchain.cn/books/geth/part7/storage.html
*/

contract Vault {
    // 插槽 0
    uint256 public count = 123;
    // 插槽 1
    address public owner = msg.sender;
    bool public isTrue = true;
    uint16 public u16 = 31;
    // 插槽 2
    bytes32 private password;

    // 常量不会被存储到存储中
    uint256 public constant someConst = 123;

    // 插槽 3, 4, 5 (数组中的元素依次排列)
    bytes32[3] public data;

    struct User {
        uint256 id;
        bytes32 password;
    }

    // 插槽 6 - 数组的长度
    // 数组的元素从 hash(6)开始
    // 存储数组元素的插槽在 keccak256(slot)) + (index * elementSize)
    // 这里 插槽为6，元素的大小为2 (1(uint256) + 1(bytes31))
    User[] private users;

    // 插槽 7 - 空
    // map映射的条目将存储再 hash(key, slot)
    // 这里 slot为 7, key为map的key
    mapping(uint256 => User) private idToUser;

    constructor(bytes32 _password) {
        password = _password;
    }

    function addUser(bytes32 _password) public {
        User memory user = User({id: users.length, password: _password});

        users.push(user);
        idToUser[user.id] = user;
    }

    function getArrayLocation(uint256 slot, uint256 index, uint256 elementSize)
        public
        pure
        returns (uint256)
    {
        return
            uint256(keccak256(abi.encodePacked(slot))) + (index * elementSize);
    }

    function getMapLocation(uint256 slot, uint256 key)
        public
        pure
        returns (uint256)
    {
        return uint256(keccak256(abi.encodePacked(key, slot)));
    }
}

/*
slot 0 - count
web3.eth.getStorageAt("0x534E4Ce0ffF779513793cfd70308AF195827BD31", 0, console.log)
slot 1 - u16, isTrue, owner
web3.eth.getStorageAt("0x534E4Ce0ffF779513793cfd70308AF195827BD31", 1, console.log)
slot 2 - password
web3.eth.getStorageAt("0x534E4Ce0ffF779513793cfd70308AF195827BD31", 2, console.log)

slot 6 - array length
getArrayLocation(6, 0, 2)
web3.utils.numberToHex("111414077815863400510004064629973595961579173665589224203503662149373724986687")
Note: We can also use web3 to get data location
web3.utils.soliditySha3({ type: "uint", value: 6 })
1st user
web3.eth.getStorageAt("0x534E4Ce0ffF779513793cfd70308AF195827BD31", "0xf652222313e28459528d920b65115c16c04f3efc82aaedc97be59f3f377c0d3f", console.log)
web3.eth.getStorageAt("0x534E4Ce0ffF779513793cfd70308AF195827BD31", "0xf652222313e28459528d920b65115c16c04f3efc82aaedc97be59f3f377c0d40", console.log)
Note: use web3.toAscii to convert bytes32 to alphabet
2nd user
web3.eth.getStorageAt("0x534E4Ce0ffF779513793cfd70308AF195827BD31", "0xf652222313e28459528d920b65115c16c04f3efc82aaedc97be59f3f377c0d41", console.log)
web3.eth.getStorageAt("0x534E4Ce0ffF779513793cfd70308AF195827BD31", "0xf652222313e28459528d920b65115c16c04f3efc82aaedc97be59f3f377c0d42", console.log)

slot 7 - empty
getMapLocation(7, 1)
web3.utils.numberToHex("81222191986226809103279119994707868322855741819905904417953092666699096963112")
Note: We can also use web3 to get data location
web3.utils.soliditySha3({ type: "uint", value: 1 }, {type: "uint", value: 7})
user 1
web3.eth.getStorageAt("0x534E4Ce0ffF779513793cfd70308AF195827BD31", "0xb39221ace053465ec3453ce2b36430bd138b997ecea25c1043da0c366812b828", console.log)
web3.eth.getStorageAt("0x534E4Ce0ffF779513793cfd70308AF195827BD31", "0xb39221ace053465ec3453ce2b36430bd138b997ecea25c1043da0c366812b829", console.log)
*/

```

## 应对措施

不要在区块链上存储敏感数据。

