## 使用外部合约隐藏恶意代码

## 漏洞

在Solidity中，任何地址都可以被转换为特定合同，即使该地址上的合同并不是被转换的那个。 这可能被利用来隐藏恶意代码。让我们看看如何实现。

```solidity
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.24;

/*
假设 Alice 可以看见 Foo 和 Bar 的代码，但看不到 Mal 的代码。
对 Alice 来说，显然 Foo.callBar() 会执行 Bar.log() 内部的代码。
然而，Eve 部署了带有 Mal 地址的 Foo，这样一来调用 Foo.callBar()
实际上会执行 Mal 的代码。
*/

/*
    Eve 部署 Mal。
    Eve 部署带有 Mal 地址的 Foo。
    Alice 在阅读了代码并判断调用是安全后调用 Foo.callBar()。
    虽然 Alice 预计会执行 Bar.log()，但实际执行的是 Mal.log()。 
*/

contract Foo {
    Bar bar;

    constructor(address _bar) {
        bar = Bar(_bar);
    }

    function callBar() public {
        bar.log();
    }
}

contract Bar {
    event Log(string message);

    function log() public {
        emit Log("Bar was called");
    }
}

// 这段代码隐藏在一个单独的文件中

contract Mal {
    event Log(string message);

    // function () external {
    //     emit Log("Mal was called");
    // }

    // Actually we can execute the same exploit even if this function does
    // not exist by using the fallback
    function log() public {
        emit Log("Mal was called");
    }
}
```

## 应对措施

* 在构造函数中初始化一个新合约

* 将外部合约的地址设为公开，以便可以审核外部合约的代码

```solidity
Bar public bar;

constructor() public {
    bar = new Bar();
}
```

