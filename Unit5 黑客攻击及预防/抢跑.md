# 抢跑

## 漏洞

交易在被矿工打包到区块之前需要一些时间。攻击者可以观察交易池并发送一笔交易，在原始交易之前将其包含在区块中。这种机制可能被滥用以重新排序交易，从而使攻击者获利。

```solidity
/ SPDX-License-Identifier: MIT
pragma solidity ^0.8.24;

/*
Alice 创建了一个猜谜游戏。
如果你能找到正确的字符串并使其哈希值等于目标哈希值，你将赢得10个以太币。
让我们看看这个合约是如何受到抢跑攻击的。

*/

/*
1. Alice 部署了包含10个以太币的 FindThisHash 合约。
2. Bob 找到了能使哈希值等于目标哈希值的字符串 ("Ethereum")。
3. Bob 以 15 gwei 的油价调用 solve("Ethereum")。
4. Eve 正在观察交易池中的答案提交。
5. Eve 看到了 Bob 的答案，并以比 Bob 更高的油价(100 gwei)调用了 solve("Ethereum")。
6. Eve 的交易比 Bob 的交易更早被打包。
   Eve 赢得了10个以太币的奖励。

这是怎么回事？
交易在被打包之前需要一些时间。
未打包的交易会被放入交易池。
油价较高的交易通常会优先被打包。
攻击者可以从交易池中获取答案，并发送一笔油价更高的交易，这样他们的交易将会在原交易之前被包含到区块中。
*/

contract FindThisHash {
    bytes32 public constant hash =
        0x564ccaf7594d66b1eaaea24fe01f0585bf52ee70852af4eac0cc4b04711cd0e2;

    constructor() payable {}

    function solve(string memory solution) public {
        require(
            hash == keccak256(abi.encodePacked(solution)), "Incorrect answer"
        );

        (bool sent,) = msg.sender.call{value: 10 ether}("");
        require(sent, "Failed to send Ether");
    }
}

```

## 应对措施

* 使用提交-揭示方案 (https://medium.com/swlh/exploring-commit-reveal-schemes-on-ethereum-c4ff5a777db8)

  https://github.com/lorenzb/libsubmarine

* 使用隐私节点

## 提交-揭示方案

提交方案是一种加密算法，用于允许某人在保持值对他人隐藏的情况下承诺一个值，并且能够在之后揭示该值。提交方案中的值具有约束力，这意味着一旦提交就不能更改。该方案分为两个阶段：选择并指定一个值的提交阶段，以及揭示并检查该值的揭示阶段。

```solidity
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.24;

/*
   现在让我们看看如何使用提交揭示方案来防止抢先交易。
*/

/*
1. Alice 部署 SecuredFindThisHash 合约并存入 10 以太币。
2. Bob 找到正确的字符串，该字符串将散列到目标哈希值。（"Ethereum"）。
3. Bob 随后找到 keccak256(地址转小写 + 解决方案 + 密钥)。
   地址是他的钱包地址的小写形式，解决方案是“Ethereum”，密钥是仅 Bob 知道的“mysecret”，
   Bob 用它来提交和揭示解决方案。
   keccak256("0xf39Fd6e51aad88F6F4ce6aB8827279cffFb92266Ethereummysecret") = '0xf95b1dd61edc3bd962cdea3987c6f55bcb714a02a2c3eb73bd960d6b4387fc36'
3. Bob 然后调用 commitSolution("0xf95b1dd61edc3bd962cdea3987c6f55bcb714a02a2c3eb73bd960d6b4387fc36")，
   他提交计算出的解决方案哈希，气价设为 15 gwei。
4. Eve 监视交易池以提交答案。
5. Eve 看到 Bob 的答案，他也调用 commitSolution("0xf95b1dd61edc3bd962cdea3987c6f55bcb714a02a2c3eb73bd960d6b4387fc36")
   气价高于 Bob（100 gwei）。
6. Eve 的交易在 Bob 之前被挖矿，但 Eve 尚未获得奖励。
   他需要用确切的密钥和解决方案调用 revealSolution()，所以假设他正在监视交易池
   来抢先 Bob，就像之前一样。
7. 然后 Bob 调用 revealSolution("Ethereum", "mysecret") 并设置气价为 15 gwei；
8. 假设 Eve 也在监视交易池，他发现 Bob 的揭示解决方案交易，他也以高于 Bob 的气价（100 gwei）
   调用 revealSolution("Ethereum", "mysecret")。
9. 假设这次 Eve 的揭示交易也在 Bob 之前被挖矿，但 Eve 会
   因“哈希不匹配”错误而失败。因为 revealSolution() 函数使用 
   keccak256(msg.sender + solution + secret) 来检查哈希。因此这次 Eve 未能赢得奖励。
10. 但 Bob 的 revealSolution("Ethereum", "mysecret") 通过了哈希检查，并获得了 10 以太币的奖励。
*/

contract SecuredFindThisHash {
    // 结构体用于存储提交详情
    struct Commit {
        bytes32 solutionHash;
        uint256 commitTime;
        bool revealed;
    }

    // 需要解决的哈希
    bytes32 public hash =
        0x564ccaf7594d66b1eaaea24fe01f0585bf52ee70852af4eac0cc4b04711cd0e2;

    // 获胜者的地址
    address public winner;

    // 奖励金额
    uint256 public reward;

    // 游戏状态
    bool public ended;

    // 用于存储提交详情的映射
    mapping(address => Commit) commits;

    // 检查游戏是否活跃的修饰符
    modifier gameActive() {
        require(!ended, "Already ended");
        _;
    }

    constructor() payable {
        reward = msg.value;
    }

    /* 
       提交函数存储通过 keccak256(地址转小写 + 解决方案 + 密钥) 计算的哈希。 
       用户只能提交一次，并且只有在游戏活跃时才可以提交。
    */
    function commitSolution(bytes32 _solutionHash) public gameActive {
        Commit storage commit = commits[msg.sender];
        require(commit.commitTime == 0, "Already committed");
        commit.solutionHash = _solutionHash;
        commit.commitTime = block.timestamp;
        commit.revealed = false;
    }

    /* 
        获取提交详情的函数。返回 (solutionHash, commitTime, revealStatus) 元组；  
        只有游戏活跃且已提交解决方案哈希的用户才能获取解决方案
    */
    function getMySolution()
        public
        view
        gameActive
        returns (bytes32, uint256, bool)
    {
        Commit storage commit = commits[msg.sender];
        require(commit.commitTime != 0, "Not committed yet");
        return (commit.solutionHash, commit.commitTime, commit.revealed);
    }
    
    /* 
        揭示提交并获取奖励。 
        只有当游戏活跃且用户已在当前区块之前提交解决方案哈希并且尚未揭示时，
        才能揭示解决方案。生成一个 keccak256(msg.sender + solution + secret) 并检查它与先前提交的哈希是否匹配。  
        假设提交已经被包含在链上，由于 msg.sender 不同，抢先者将无法通过此检查。
        然后通过 keccak256(solution) 检查实际解决方案，如果解决方案匹配，则宣布获胜者，
        游戏结束，奖励金额被发送给获胜者。
    */

    function revealSolution(string memory _solution, string memory _secret)
        public
        gameActive
    {
        Commit storage commit = commits[msg.sender];
        require(commit.commitTime != 0, "Not committed yet");
        require(
            commit.commitTime < block.timestamp,
            "Cannot reveal in the same block"
        );
        require(!commit.revealed, "Already commited and revealed");

        bytes32 solutionHash =
            keccak256(abi.encodePacked(msg.sender, _solution, _secret));
        require(solutionHash == commit.solutionHash, "Hash doesn't match");

        require(
            keccak256(abi.encodePacked(_solution)) == hash, "Incorrect answer"
        );

        winner = msg.sender;
        ended = true;

        (bool sent,) = payable(msg.sender).call{value: reward}("");
        if (!sent) {
            winner = address(0);
            ended = false;
            revert("Failed to send ether.");
        }
    }
}

```

