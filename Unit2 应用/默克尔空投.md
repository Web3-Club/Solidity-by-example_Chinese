# 默克尔空投

使用默克尔树的空投合约的例子

```solidity
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.24;

import {MerkleProof} from "./MerkleProof.sol";

interface IToken {
    function mint(address to, uint256 amount) external;
}

contract Airdrop {
    event Claim(address to, uint256 amount);

    IToken public immutable token;
    bytes32 public immutable root;
    mapping(bytes32 => bool) public claimed;

    constructor(address _token, bytes32 _root) {
        token = IToken(_token);
        root = _root;
    }

    function getLeafHash(address to, uint256 amount)
        public
        pure
        returns (bytes32)
    {
        return keccak256(abi.encode(to, amount));
    }

    function claim(bytes32[] memory proof, address to, uint256 amount)
        external
    {
        // 注:(to, amount)不能有重复项
        bytes32 leaf = getLeafHash(to, amount);

        require(!claimed[leaf], "airdrop already claimed");
        require(MerkleProof.verify(proof, root, leaf), "invalid merkle proof");
        claimed[leaf] = true;

        token.mint(to, amount);

        emit Claim(to, amount);
    }
}
```

## Token

```solidity
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.24;

// ERC20 + mint(铸造) + authorization(授权)

contract Token {
    event Transfer(address indexed from, address indexed to, uint256 value);
    event Approval(
        address indexed owner, address indexed spender, uint256 value
    );

    uint256 public totalSupply;
    mapping(address => uint256) public balanceOf;
    mapping(address => mapping(address => uint256)) public allowance;
    string public name;
    string public symbol;
    uint8 public decimals;
    mapping(address => bool) public authorized;

    constructor(string memory _name, string memory _symbol, uint8 _decimals) {
        name = _name;
        symbol = _symbol;
        decimals = _decimals;
        authorized[msg.sender] = true;
    }

    function setAuthorized(address addr, bool auth) external {
        require(authorized[msg.sender], "not authorized");
        authorized[addr] = auth;
    }

    function transfer(address recipient, uint256 amount)
        external
        returns (bool)
    {
        balanceOf[msg.sender] -= amount;
        balanceOf[recipient] += amount;
        emit Transfer(msg.sender, recipient, amount);
        return true;
    }

    function approve(address spender, uint256 amount) external returns (bool) {
        allowance[msg.sender][spender] = amount;
        emit Approval(msg.sender, spender, amount);
        return true;
    }

    function transferFrom(address sender, address recipient, uint256 amount)
        external
        returns (bool)
    {
        allowance[sender][msg.sender] -= amount;
        balanceOf[sender] -= amount;
        balanceOf[recipient] += amount;
        emit Transfer(sender, recipient, amount);
        return true;
    }

    function _mint(address to, uint256 amount) internal {
        balanceOf[to] += amount;
        totalSupply += amount;
        emit Transfer(address(0), to, amount);
    }

    function mint(address to, uint256 amount) external {
        require(authorized[msg.sender], "not authorized");
        _mint(to, amount);
    }
}
```


## 从 OpenZeppelin 复制的库

```solidity
// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts

pragma solidity ^0.8.20;

import {Hashes} from "./Hashes.sol";

library MerkleProof {
    function verify(bytes32[] memory proof, bytes32 root, bytes32 leaf)
        internal
        pure
        returns (bool)
    {
        return processProof(proof, leaf) == root;
    }

    function processProof(bytes32[] memory proof, bytes32 leaf)
        internal
        pure
        returns (bytes32)
    {
        bytes32 computedHash = leaf;
        for (uint256 i = 0; i < proof.length; i++) {
            computedHash = Hashes.commutativeKeccak256(computedHash, proof[i]);
        }
        return computedHash;
    }
}

// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts

pragma solidity ^0.8.0;

library Hashes {
    function commutativeKeccak256(bytes32 a, bytes32 b)
        internal
        pure
        returns (bytes32)
    {
        return a < b ? _efficientKeccak256(a, b) : _efficientKeccak256(b, a);
    }

    function _efficientKeccak256(bytes32 a, bytes32 b)
        private
        pure
        returns (bytes32 value)
    {
        /// @solidity memory-safe-assembly
        assembly {
            mstore(0x00, a)
            mstore(0x20, b)
            value := keccak256(0x00, 0x40)
        }
    }
}
```


## 测试

```solidity
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.24;

contract MerkleHelper {
    // 将证明存储在状态变量中，因为数据不能推送到动态内存数组中
    bytes32[] public proof;

    // 警告-改变哈希数组
    function calcRoot(bytes32[] memory hashes) public pure returns (bytes32) {
        uint256 n = hashes.length;

        while (n > 1) {
            for (uint256 i = 0; i < n; i += 2) {
                bytes32 left = hashes[i];
                bytes32 right = hashes[i + 1 < n ? i + 1 : i];
                (left, right) = left <= right ? (left, right) : (right, left);
                hashes[i / 2] = keccak256(abi.encode(left, right));
            }
            n = (n + (n & 1)) / 2;
        }

        return hashes[0];
    }

    // 警告-改变哈希数组
    function getProof(bytes32[] memory hashes, uint256 index)
        public
        returns (bytes32[] memory)
    {
        // 重置证明
        delete proof;

        uint256 n = hashes.length;
        uint256 k = index;

        while (n > 1) {
            // 为这一级获取证明
            uint256 j = k & 1 == 1 ? k - 1 : (k + 1 < n ? k + 1 : k);
            bytes32 h = hashes[j];
            proof.push(h);
            k /= 2;

            // 计算下一级的哈希
            for (uint256 i = 0; i < n; i += 2) {
                bytes32 left = hashes[i];
                bytes32 right = hashes[i + 1 < n ? i + 1 : i];
                (left, right) = left <= right ? (left, right) : (right, left);
                hashes[i / 2] = keccak256(abi.encode(left, right));
            }
            n = (n + (n & 1)) / 2;
        }

        return proof;
    }
}

// SPDX-License-Identifier: MIT
pragma solidity ^0.8.24;

import {Test, console2} from "forge-std/Test.sol";
import {MerkleHelper} from "./MerkleHelper.sol";
import {Airdrop} from "../../../src/app/airdrop/Airdrop.sol";
import {Token} from "../../../src/app/airdrop/Token.sol";

contract AirdropTest is Test {
    Token private token;
    Airdrop private airdrop;
    MerkleHelper private merkleHelper;

    address[] private users;
    uint256[] private amounts;
    bytes32[] private hashes;

    uint256 constant N = 100;

    function setUp() public {
        merkleHelper = new MerkleHelper();
        token = new Token("test", "TEST", 18);

        // 初始化用户和空投数量
        for (uint256 i = 1; i <= N; i++) {
            users.push(address(uint160(i)));
            amounts.push(i * 100);
        }

        // 计算叶子节点的哈希值
        for (uint256 i = 0; i < N; i++) {
            hashes.push(keccak256(abi.encode(users[i], amounts[i])));
        }

        bytes32 root = merkleHelper.calcRoot(hashes);

        airdrop = new Airdrop(address(token), root);

        token.setAuthorized(address(airdrop), true);
    }

    function test_valid_proof() public {
        for (uint256 i = 0; i < N; i++) {
            bytes32[] memory proof = merkleHelper.getProof(hashes, i);
            airdrop.claim(proof, users[i], amounts[i]);
            assertEq(token.balanceOf(users[i]), amounts[i]);
        }
    }

    function test_invalid_proof() public {
        bytes32[] memory proof = merkleHelper.getProof(hashes, 0);
        vm.expectRevert();
        airdrop.claim(proof, users[1], amounts[1]);
    }

    function test_claim_twice() public {
        bytes32[] memory proof = merkleHelper.getProof(hashes, 0);
        airdrop.claim(proof, users[0], amounts[0]);

        vm.expectRevert();
        airdrop.claim(proof, users[0], amounts[0]);
    }
}
```